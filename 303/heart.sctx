scchart heart {
  
  input signal as, vs
  output signal ap, vp
  
  const int VRP_time = 150
  const int PVARP_time = 50
  const int URI_time = 900
  const int LRI_time = 950
  const int AEI_time = 800
  const int AVI_time = 300
  
  
  region {
    
    signal vsi, asi, vr, ar, vp_o
    int ready = 0

    initial state heart{
      region{
        clock VRP
        
        initial state start
          immediate do VRP = 0 go to waiting
        
        state waiting
          if VRP > VRP_time go to listening
          if vs do vr go to waiting
        
        state listening
          if vs do vsi go to listening
        
       }
       
      region{
        clock PVARP
        
        initial state start
          immediate do PVARP = 0 go to waiting
        
        state waiting
          if PVARP > PVARP_time go to listening
          if as do ar go to waiting
        
        state listening
          if as do asi go to listening
        
       }
     
      region{
        clock URI, LRI
        
        initial state start
          immediate do URI = 0; LRI = 0; ready = 0 go to waiting
          
        state waiting
          if URI > URI_time do ready = 1 go to waiting_vs
          
        state waiting_vs
          if LRI > LRI_time do vp_o go to end
          
        final state end
        
      }
      
      region{
        clock AEI, AVI

        initial state start
          immediate do AEI = 0 go to waiting_aei
          
        state waiting_aei
          if asi do AVI = 0 go to waiting_avi
          
          if AEI > AEI_time do AVI = 0; ap go to waiting_avi
        
        state waiting_avi
          if AVI > AVI_time && ready == 1 do vp_o go to end
          
         final state end
          
      }
     }
      
     if vsi abort to heart
     if vp_o do vp abort to heart
   }
   
  
}
